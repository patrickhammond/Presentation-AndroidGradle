buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'findbugs'
apply plugin: 'com.github.ben-manes.versions'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "com.mycompany.myapp"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "BUILD_TIME", sprintf('\"%s\"', new Date().toString())
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        resConfigs "en"
        resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"
    }

    productFlavors {
        lite {
            applicationId 'com.mycompany.myapp.lite'
        }
        paid {
            applicationId 'com.mycompany.myapp.paid'
        }
    }

    signingConfigs {
        release {
            storeFile file('release.jks')
            storePassword 'release'
            keyAlias 'release'
            keyPassword 'release'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-google-play-services.pro'

            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        lintConfig file('lint.xml')
        htmlReport true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services-base:6.5.87'
}

task findbugs(type: FindBugs, dependsOn: assembleDebug) {
    excludeFilter file('findbugs-exclude.xml')
    classes = fileTree('build/intermediates/classes/lite/debug/')  // Must pick a build variant and type
    source = fileTree('src/main/java/')
    classpath = files()
    effort = 'max'
    reports {
        xml.enabled = false
        html.enabled = true
    }
}